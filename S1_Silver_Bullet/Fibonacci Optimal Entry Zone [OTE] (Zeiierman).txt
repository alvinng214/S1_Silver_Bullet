// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// © Zeiierman

//@version=6
indicator('Fibonacci Optimal Entry Zone [OTE] (Zeiierman)', overlay = true, max_bars_back = 5000, max_labels_count = 500, max_lines_count = 500)

// ~~ Tooltips {
string t1 = "Defines how many bars back the pivot high/low is calculated from. Higher values detect stronger swing structures."
string t2 = "Enable to display bullish market structure (higher highs and higher lows)."
string t3 = "Select the color of bullish structure lines and labels."
string t4 = "Enable to display bearish market structure (lower highs and lower lows)."
string t5 = "Select the color of bearish structure lines and labels."
string t6 = "Width of the Break of Structure (BoS) line to make it more visible on the chart."
string t7 = "Enable automatic tracking of recent swing points. Adjusts Fibonacci levels in real-time as new swings form."
string t8 = "Enable to draw dotted lines connecting swing highs/lows (the swing trend line)."
string t9 = "Adjust the thickness of the swing trend line. Useful for emphasizing or de-emphasizing swing connections."
string t10 = "Enable to show price labels at swing highs/lows. Helps visualize turning points with exact prices."
string t11 = "Show previous (historical) Fibonacci levels instead of clearing them each time new structure forms."
string t12 = "Enable to keep Fibonacci levels extended forward to the current bar. Helps maintain context while trading."
string t13 = "Enable to fill the Golden Zone (typically 0.5 - 0.618 retracement) between the first two Fibonacci levels."
string t14 = "Select the fill color for the Golden Zone. Softer transparency is ideal to avoid chart clutter."
string t15 = "Enable or disable individual Fibonacci levels. Checked levels will be drawn on the chart."
string t16 = "Customize the specific Fibonacci retracement level value. 0.50 is midpoint, 0.618 is golden ratio."
string t17 = "Choose the color for each Fibonacci level line. Use contrasting colors for clarity."
string t18 = "Sets the thickness of the Fibonacci level lines. Increase for better visibility."
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Inputs {
prd   = input.int(10, minval = 1, title = 'Structure Period', group="Structure", tooltip = t1)
bull  = input.bool(true, 'Bullish Structure     ', group="Structure", inline = 'Bullish', tooltip = t2)
bull2 = input.color(#08ec32, '', group="Structure", inline = 'Bullish', tooltip = t3)
bear  = input.bool(true, 'Bearish Structure    ', group="Structure", inline = 'Bearish', tooltip = t4)
bear2 = input.color(color.rgb(255, 34, 34), title="",group="Structure", inline = 'Bearish', tooltip = t5)
s_width = input.int(1, minval=1, maxval= 10, group="Structure", title = 'BoS Width', inline = 'width', tooltip = t6)

follow       = input.bool(true, 'Swing tracker', group="Fibonacci Mode", inline = 'fibbinput', tooltip = t7)
swingline    = input.bool(true,'Swing Line', group="Fibonacci Mode", inline = 'fibbinput1', tooltip = t8)
swline_width = input.int(2, minval=1, maxval= 10, title=" ", group="Fibonacci Mode", inline = 'fibbinput1', tooltip = t9)
swinglab   = input.bool(true,'Swing Lables', group="Fibonacci Mode", inline = 'fibbinput2', tooltip = t10)
showOld    = input.bool(false, 'Previous', group="Fibonacci", inline = 'fibbinput', tooltip = t11)
extend     = input.bool(true, 'Extend', group="Fibonacci", inline = 'fibbinput', tooltip = t11 + "\n\n" +t12)

golden   = input.bool(false, title='Fill Golden Zone', group="Fibonacci", inline = 'golden', tooltip = t13)
goldZone = input.color(color.new(color.teal,50), title="", group="Fibonacci", inline = 'golden', tooltip = t14)

var array<bool> levelsBool = array.from(input.bool(true, title = '', inline = '1', tooltip = t15), input.bool(true, title = '', inline = '2', tooltip = t15))
var array<float> inputLevels = array.from(input.float(0.50, title = '', step = .1, inline = '1', tooltip = t16), input.float(0.618, title = '', step = .1, inline = '2', tooltip = t16))
var array<color> levelsCol = array.from(input.color(color.new(#4CAF50, 0), title = '', inline = '1', tooltip = t17), input.color(color.new(#009688, 0), title = '', inline = '2', tooltip = t17))
fibb_width = input.int(1, minval=1, maxval= 10, title = 'Fibb Width', inline = 'width_', tooltip = t18)
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Variables {
b = bar_index
var pos = 0

var Up = float(na)
var Dn = float(na)
var iUp = int(na)
var iDn = int(na)

var swingLow = float(na)
var swingHigh = float(na)
var iswingLow = int(na)
var iswingHigh = int(na)

var array<float> levels = array.new<float>()
var array<color> colors = array.new<color>()
var array<line> flevels = array.new<line>()
var lf = linefill(na)
var line trend = line(na)

if levels.size() == 0
    for i = 0 to levelsBool.size() - 1 by 1
        if levelsBool.get(i)
            levels.push(inputLevels.get(i))
            colors.unshift(levelsCol.get(i))
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Functions {
//Labels
CreateLabel(x, y, txt, col, z) =>
    label.new(x, y, txt, textcolor = col, style = z ? label.style_label_down : label.style_label_up, color = color(na))
    //Lines
CreateLine(x1, x2, y, col, w) =>
    line.new(x1, x2, b, y, color = col, width=w)
    //Update
UpdateLine(l, y, i) =>
    l.set_xy1(i, y)
    l.set_xy2(b, y)

//Fibb Calc
fibb(v, h, l, ih, il) =>
    x = h
    y = l
    f = float(na)
    if il < ih
        f := x - (x - y) * v
        f
    if il > ih
        f := y + (x - y) * v
        f
    f
    //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Pivots {
Up := math.max(Up[1], high)
Dn := math.min(Dn[1], low)
pvtHi = ta.pivothigh(high, prd, prd)
pvtLo = ta.pivotlow(low, prd, prd)
if not na(pvtHi) and pos <= 0
    Up := pvtHi
    Up
if not na(pvtLo) and pos >= 0
    Dn := pvtLo
    Dn
    //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

var label labelStart = na
var label labelEndu = na
var label labelEndd = na

// ~~ Structure {
// ~~ Structure {
if Up > Up[1]
    iUp := b
    centerBull = math.round(math.avg(iUp[1], b))
    if pos <= 0
        if not showOld
            trend.delete()
            for l in flevels
                l.delete()
        flevels.clear()
        if bull
            CreateLabel(centerBull, Up[1], 'CHoCH', bull2, true)
            CreateLine(iUp[1], Up[1], Up[1], bull2, s_width)
            for [i, l] in levels
                val = fibb(l, Up, Dn, iUp, iDn)
                ll = CreateLine(iDn, val, val, colors.get(i), fibb_width)
                flevels.unshift(ll)
            if swingline    
                trend := line.new(iDn, Dn, b, Up, color = bull2, style = line.style_dotted, width = swline_width)
            if swinglab    
                label.delete(labelEndu)
                labelEndu := CreateLabel(b, Up, str.tostring(Up, "#.##"), chart.fg_color, true)
        pos := 1
        swingLow := Dn
        iswingLow := iDn
    else if pos == 1 and Up > Up[1]
        if flevels.size() > 0 and bull
            for [i, l] in levels
                val = follow ? fibb(l, Up, Dn, iUp, iDn) : fibb(l, Up, swingLow, iUp, iswingLow)
                UpdateLine(flevels.get(i), val, follow ? iDn : iswingLow)
            if follow
                trend.set_xy1(iDn, Dn)
            trend.set_xy2(b, Up)
            if swinglab 
                label.delete(labelEndu)
                labelEndu := CreateLabel(b, Up, str.tostring(Up, "#.##"), chart.fg_color, true)
        pos := 2
    else if pos > 1 and Up > Up[1]
        if flevels.size() > 0 and bull
            for [i, l] in levels
                val = follow ? fibb(l, Up, Dn, iUp, iDn) : fibb(l, Up, swingLow, iUp, iswingLow)
                UpdateLine(flevels.get(i), val, follow ? iDn : iswingLow)
            if follow
                trend.set_xy1(iDn, Dn)
            if swinglab     
                label.delete(labelStart)
                labelStart := CreateLabel(iDn, Dn, str.tostring(Dn, "#.##"), chart.fg_color, false)
            trend.set_xy2(b, Up)
            if swinglab 
                label.delete(labelEndu)
                labelEndu := CreateLabel(b, Up, str.tostring(Up, "#.##"), chart.fg_color, true)
        pos := pos + 1
else if Up < Up[1]
    iUp := b - prd
if Dn < Dn[1]
    iDn := b
    centerBear = math.round(math.avg(iDn[1], b))
    if pos >= 0
        if not showOld
            trend.delete()
            for l in flevels
                l.delete()
        flevels.clear()
        if bear
            CreateLabel(centerBear, Dn[1], 'CHoCH', bear2, false)
            CreateLine(iDn[1], Dn[1], Dn[1], bear2, s_width)
            for [i, l] in levels
                val = fibb(l, Dn, Up, iDn, iUp)
                ll = CreateLine(iUp, val, val, colors.get(i), fibb_width)
                flevels.unshift(ll)
            if swingline    
                trend := line.new(iUp, Up, b, Dn, color = bear2 ,style = line.style_dotted, width = swline_width)
            if swinglab     
                label.delete(labelEndd)
                labelEndd := CreateLabel(b, Dn, str.tostring(Dn, "#.##"), chart.fg_color, false)
        pos := -1
        swingHigh := Up
        iswingHigh := iUp
    else if pos == -1 and Dn < Dn[1]
        if flevels.size() > 0 and bear
            for [i, l] in levels
                val = follow ? fibb(l, Up, Dn, iUp, iDn) : fibb(l, Dn, swingHigh, iDn, iswingHigh)
                UpdateLine(flevels.get(i), val, follow ? iUp : iswingHigh)
            if follow
                trend.set_xy1(iUp, Up)
            trend.set_xy2(b, Dn)
        pos := -2
    else if pos < -1 and Dn < Dn[1]
        if flevels.size() > 0 and bear
            for [i, l] in levels
                val = follow ? fibb(l, Up, Dn, iUp, iDn) : fibb(l, Dn, swingHigh, iDn, iswingHigh)
                UpdateLine(flevels.get(i), val, follow ? iUp : iswingHigh)
            if follow
                trend.set_xy1(iUp, Up)
            if swinglab     
                label.delete(labelStart)
                labelStart := CreateLabel(iUp, Up, str.tostring(Up, "#.##"), chart.fg_color, true)
            trend.set_xy2(b, Dn)
            if swinglab 
                label.delete(labelEndd)
                labelEndd := CreateLabel(b, Dn, str.tostring(Dn, "#.##"), chart.fg_color, false)
        pos := pos - 1
else if Dn > Dn[1]
    iDn := b - prd
    //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//Real-time fibb Update
if extend
    if pos >= 0 and bull and flevels.size() > 0
        for l in flevels
            l.set_x2(b)
    if pos <= 0 and bear and flevels.size() > 0
        for l in flevels
            l.set_x2(b)

//Golden Linefill
if golden and flevels.size() > 1
    linefill.new(flevels.get(0), flevels.get(1), goldZone)